.nolist
;Copyright (c) 2009, Luc BRUANT aka fullmetalcoder.
;Copyright (c) 2004, Sean McLaughlin.
;Redistribution and use in source and binary forms, with or without modification, are
;permitted provided that the following conditions are met:
;Redistributions of source code must retain the above copyright notice, this list of
;conditions and the following disclaimer.
;
;Redistributions in binary form must reproduce the above copyright notice, this list of
;conditions and the following disclaimer in the documentation and/or other materials
;provided with the distribution.
;
;THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
;"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
;TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
;PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR
;CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
;EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
;PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
;PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
;OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
;NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
;SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
.list

flash_strcpy:
	in	a, (map_bankA_port)
	push	af
	ld	a, $1C
	call	zp.setflashpage
	
	call	zp.strcpy
	
	pop	af
	out	(map_bankA_port), a
	ret

; brief : displays a pretty register dump
; input : none
; output : none
; destroys : none
vect_regdump:
	push	af
	push	bc
	push	de
	push	hl
	push	ix
	push	iy
	
	; stack frame :
	;	* iy
	;	* ix
	;	* hl
	;	* de
	;	* bc
	;	* af
	;	* return address (to _throw)
	;	* return address (to _throw's caller)
	;	... (frame prior to _throw)
	
	ld	hl, 16
	add	hl, sp
	push	hl
	dec	hl
	dec	hl
	deref_hl
	dec	hl
	dec	hl
	dec	hl
	push	hl
	ld	a, i
	ld	h, a
	ld	a, r
	ld	l, a
	push	hl
	
	call	zp.cclear
	
	ld	hl, str_exception
	ld	de, os.console
	call	flash_strcpy
	
	ld	b, 9
	ld	hl, 0
	add	hl, sp
 	ld	de, os.console + (3*24) + 19
	
vect_regdump.loop:
	; print MSB
	inc	hl
	call	zp.memdump
	; print LSB
	dec	hl
	call	zp.memdump
	; go to next value
	inc	hl
	inc	hl
	; update ascii copy target
	ld	a, e
	sub	12
	jr	nc, vect_regdump.noadjust
	neg
	dec	d
vect_regdump.noadjust:
	ld	e, a
	
	djnz	vect_regdump.loop
	
	; skip stack entry : caller
	inc	hl
	inc	hl
	
	; dump stack a bit now...
	
	ld	b, 16
 	ld	de, os.console + (5*24) - 1
	
	; remove zero terminator
	ld	a, ' '
	ld	(de), a
	
vect_stackdump.loop:
	; update ascii copy target
	inc	de
	inc	de
	
	; print MSB
	inc	hl
	call	zp.memdump
	; print LSB
	dec	hl
	call	zp.memdump
	; go to next value
	inc	hl
	inc	hl
	
	; break away
	ld	a, h
	or	l
	jr	z, $+4
	
	djnz	vect_stackdump.loop
	
	xor	a
	ld	(de), a
	
	; remove reg dump from stack
	ld	hl, 10
	add	hl, sp
	ld	sp, hl
	
	; restore all registers used by the routine
	pop	hl
	pop	de
	pop	bc
	pop	af
	ret

vect_tcreate:
	push	af
	ld	a, E_CREATE
	jr	_throw

vect_tlocate:
	push	af
	ld	a, E_LOCATE
	jr	_throw

vect_tlink:
	push	af
	ld	a, E_LINK
	jr	_throw

vect_texist:
	push	af
	ld	a, E_EXIST
	jr	_throw

vect_tmemory:
	push	af
	ld	a, E_MEMORY
	jr	_throw

vect_tchecksum:
	push	af
	ld	a, E_CHECKSUM
	jr	_throw

vect_tcompat:
	push	af
	ld	a, E_COMPAT
	jr	_throw

vect_throw:
	push	af
	ld	a, $FF

_throw:
	; let's avoid recursion huh (and avoid rendering the dumps useless...)
	di
	
	ld	(os.exception), a
	pop	af
	
	push	af
	
	ld	a, (os.rampages)
	inc	a
	call	z, _throw.save
	
	pop	af
	
	; display regdump
	call	vect_regdump
	
	push	af
	push	bc
	push	de
	push	hl
	
	; display exception type string (default : unhandled)
	ld	de, os.console
	ld	a, (os.exception)
	ld	hl, str_eunhandled
	
	; check for known type
	cp	E_LAST+1
	jr	nc, _throw_unhandled
	
	; pick right string
	inc	de
	inc	de
	push	af
	call	zp.htoa_byte
	pop	af
	inc	de
	
	ld	hl, err_strings
	add	a, a
	offset_hl
	deref_hl
	
_throw_unhandled:
	call	flash_strcpy
	
	; remove ugly zero terminator from console...
	dec	de
	ld	a, ' '
	ld	(de), a
	
_throw_show:
	; display the console
	call	zp.cdisp
	
	ld	hl, os.console + 24
	ld	c, 1
	call	_throw_next
	
_throw_update:
	
	; display address selection indicator
	call _throw_disp
	
	; wait for key input
_throw_wait:
	; a bit of delay is in order and we can't afford to halt since
	; we have disabled interrupts and do not want to reenable them now
	ld	c, 100
	call	zp.timer_msleep
	
	call	zp.getkey
	cp	sk_clear
	jp	z, _throw_return
	cp	sk_enter
	jp	z, _throw_hexedit
	cp	sk_up
	jr	z, _throw_up
	cp	sk_down
	jr	z, _throw_down
	cp	sk_left
	jp	z, _throw_left
	cp	sk_right
	jp	z, _throw_right
	cp	sk_mode
	jr	nz, _throw_wait
	
	; return to console
	jp zp.console

_throw_move:
	ex	(sp), hl
	call	_throw_disp
	pop	hl
	jr	_throw_update

_throw_nomove
	pop	hl
	jr	_throw_wait

_throw_up:
	push	hl
	ld	de, -25
	add	hl, de
	ld	c, 1
	call	_throw_next
	;jr	nc, _throw_nomove
	jr	_throw_move

_throw_down:
	push	hl
	ld	de, 25
	add	hl, de
	ld	c, 0
	call	_throw_next
	;jr	nc, _throw_nomove
	jr	_throw_move

_throw_left:
	ld	c, 0
	push	hl
	call	_throw_next
	jr	c, _throw_nomove
	jr	_throw_move

_throw_right:
	ld	c, 1
	push	hl
	call	_throw_next
	jr	c, _throw_nomove
	jr	_throw_move


; c = 1 : ++, c = 0 : --
_throw_next:
	bit	0, c
	jr	z, _throw_next.dec
	
	ld	de, os.console + (24*9)
	cmp_hlde
	ccf
	ret	c
	inc	hl
	jr	_throw_next.ok
	
_throw_next.dec:
	ld	de, os.console + 24
	cmp_hlde
	ret	c
	dec	hl
	
_throw_next.ok:
	ld	a, (hl)
	cp	'0'
	jr	c, _throw_next
	cp	'9'+1
	jr	c, _throw_check
	cp	'A'
	jr	c, _throw_next
	cp	'F'+1
	jr	c, _throw_check
; 	cp	'a'
; 	jr	c, _throw_next
; 	cp	'f'+1
; 	ret	c
	jr	_throw_next

_throw_check:
	dec	hl
	ld	a, ' '
	cp	(hl)
	inc	hl
	jr	c, _throw_next
	ld	de, 4
	push	hl
	add	hl, de
	cp	(hl)
	pop	hl
	ret	nc
	jr	_throw_next

_throw_disp:
	push	hl
	ld	de, -os.console
	add	hl, de
	
	ld	c, 24
	call	zp.div_hl_c
	
	ld	c, a
	
	ld	a, l
	add	a, a
	add	a, l
	add	a, a
	ld	b, a
	
	ld	de, $0604
	call	vect_cinvert
	
	pop	hl
	ret

; return to caller, *virtually* unharmed (console and screen have been altered...)
_throw_return:
	; wait for no key to be pressed
	call	zp.getkey
	cp	sk_null
	jr	nz, $-5
	
	; clear keyport to avoid unwanted keypress to be caught by interrupted code
	;ld	a, $FF
	;out	(key_port), a
	
	ld	a, (os.rampages)
	inc	a
	call	z, _throw.restore
	
	; restore regs
	pop	hl
	pop	de
	pop	bc
	pop	af
	
	; reenable interrupts
	ei
	
	; go back
	ret

; open hexedit to view some memory (starting from selected location)
_throw_hexedit:
	; set parameters
	
	push	hl
	push	hl
	; ensure zero termination
	ld	bc, 4
	add	hl, bc
	ld	(hl), 0
	pop	hl
	
	ex	de, hl
	ld	hl, (os.keymap.normal)
	push	hl
	ld	hl, (os.keymap.shift)
	push	hl
	ld	hl, (os.keymap.alpha)
	push	hl
	
	; use default sys vbuf
	ld	hl, (os.vbuf_addr)
	push	hl
	ld	hl, os.lcd.mem
	ld	(os.vbuf_addr), hl
	
	; restore default keymap just in case...
	ld	hl, 0
	ld	(os.keymap.normal), hl
	ld	(os.keymap.shift), hl
	ld	(os.keymap.alpha), hl
	
	ld	hl, os.flags + osf.general
	res	tios_emulation, (hl)
	
	ex	de, hl
	
	ld	a, 1
	call	app.hexedit
	
	; restore vbuf addr
	pop	hl
	ld	(os.vbuf_addr), hl
	
	; restore keymaps
	pop	hl
	ld	(os.keymap.normal), hl
	pop	hl
	ld	(os.keymap.shift), hl
	pop	hl
	ld	(os.keymap.alpha), hl
	
	pop	hl
	di
	call	zp.cdisp
	jp	_throw_update

_throw.save:
	push	bc
	push	de
	push	hl
	; dump some memory that is about to be used to page 5 to restore it
	; if user chosses to resume program execution
	in	a, (map_bankA_port)
	push	af
	ld	a, $85
	out	(map_bankA_port), a
	
	ld	hl, os.sysram
	ld	de, $4000
	ld	bc, os.command - os.sysram
	ldir
	
	; oh and take a screenshot as well to restore it...
	ex	de, hl
	call	zp.screenshot
	
	pop	af
	out	(map_bankA_port), a
	
	pop	hl
	pop	de
	pop	bc
	ret

_throw.restore:
	in	a, (map_bankA_port)
	push	af
	ld	a, $85
	out	(map_bankA_port), a
	
	; restore previously saved sysram
	ld	hl, $4000
	ld	de, os.sysram
	ld	bc, os.command - os.sysram
	ldir
	
	ex	de, hl
	; restore LCD memory from screenshot
	; save vbuf addr
	ld	hl, (os.vbuf_addr)
	
	; pretend our screenshot is vbuf
	ex	de, hl
	ld	(os.vbuf_addr), hl
	
	; restore LCD memory
	call	zp.vbufdisp
	
	; restore vbuf addr
	ex	de, hl
	ld	(os.vbuf_addr), hl
	
	pop	af
	out	(map_bankA_port), a
	ret

err_strings:
	.dw	str_ecreate
	.dw	str_elocate
	.dw	str_elink
	.dw	str_eexist
	.dw	str_emem
	.dw	str_echecksum
	.dw	str_ecompat
